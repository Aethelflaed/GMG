%{

#include <string.h>
#include <stdlib.h>
#include <ctype.h>

#include <iostream>

#include "Parser.h"
#include "Makefile/Builder.hpp"

extern "C"
{
	int yyparse();
    int yylex();

    int yywrap()
    {
        return 1;
    }

	extern char* yytext;

	void yyerror(const char* str)
	{
		std::cerr << "Error : \"" << str << "\"" << std::endl;
		std::cerr << "Scanning : \"" << yytext << "\"" << std::endl;
	}
}

%}

%union
{
	char string[256];
	int integer;
	int boolean;
	void* pointer;
	char* charPointer;
}

%token T_EXIT
%token T_HELP

%token T_LIST

%token T_OPTION
%token T_SET
%token <string>T_BOOL_OPTION
%token <boolean> T_BOOL_VALUE

%token T_TARGET
%token T_ADD
%token T_EDIT
%token T_RM

%token T_END
%token T_MODULE
%token T_VERSION
%token T_SHOW

%token T_TYPE
%token <integer>T_TARGET_TYPE

%token <string>T_FILE
%token <string>T_NUMBER
%token <string>T_WORD

%type <integer>help_command

%type <string>quoted_name
%type <string>quoted_file
%type <boolean>bool_value

%start configurations

%%

configurations: /* empty */
			  | configurations configuration

configuration: target
			 | option
			 | configuration_help
			 | exit

exit: T_EXIT
   {
		if (Parser::getParser().isInteractive())
		{
			std::cout << "Bye!" << std::endl;
		}
		exit(0);
   }

configuration_help: T_HELP help_command
	{
		if (Parser::getParser().isInteractive())
		{
			int command = $2;
#			include "grammar_help_part.h"
		}
	}

help_command: /* empty */	{ $$ = 0			}
			| T_TARGET		{ $$ = T_TARGET		}
			| T_OPTION		{ $$ = T_OPTION		}

option: T_OPTION option_command
	  {
	  }

option_command: T_SET option_set
			  | T_LIST
			{
				if (Parser::getParser().isInteractive())
				{
					std::cout << Parser::getParser()
										.getMakefile()
										.getOptions() << std::endl;
				}
			}

option_set: T_BOOL_OPTION bool_value
		  {
				if (strcmp($1, "debug") == 0)
				{
					Parser::getParser()
							.getMakefile()
							.getOptions().setDebug($2);
				}
				else if (strcmp($1, "verbose") == 0)
				{
					Parser::getParser()
							.getMakefile()
							.getOptions().setVerbose($2);
				}
		  }

target:	T_TARGET target_command
	  | target_mode

target_mode: T_END
		   {
				Parser::getParser().setTarget(nullptr);
				Parser::getParser().setState(INITIAL);
		   }
		   | T_SHOW
		   {
			if (Parser::getParser().isInteractive())
			{
				std::cout << Parser::getParser().getTarget() << std::endl;
			}
		   }
		   | T_SET target_mode_param
		   | T_MODULE target_mode_modules

target_mode_param: T_VERSION T_NUMBER
				 {
						Parser::getParser().getTarget().setVersion($2);
				 }
				 | T_TYPE T_TARGET_TYPE
				 {
						Parser::getParser().getTarget().setType(Makefile::TargetType($2));
				 }

target_mode_modules: T_ADD quoted_file
				   {
						Parser::getParser().getTarget().addModule($2);
				   }
				   | T_RM quoted_file
				   {
						try
						{
							Parser::getParser().getTarget().removeModule($2);
						}
						catch (const std::out_of_range& ex)
						{
							std::cerr << ex.what() << std::endl;
						}
				   }

target_command: target_add
			  | target_edit
			  | target_rm
			  | target_list

target_add: T_ADD quoted_name
		  {
				try
				{
					Parser& parser = Parser::getParser();
					parser.setTarget(&parser.getMakefile().addTarget($2));
					parser.setState(TARGET);
				}
				catch (const std::invalid_argument& ex)
				{
					std::cerr << ex.what() << std::endl;
				}
		  }

target_edit: T_EDIT quoted_name
		   {
				try
				{
					Parser& parser = Parser::getParser();
					parser.setTarget(&parser.getMakefile().getTarget($2));
					parser.setState(TARGET);
				}
				catch (const std::out_of_range& ex)
				{
					std::cerr << ex.what() << std::endl;
				}
		   }

target_rm: T_RM quoted_name
		 {
				try
				{
					Parser::getParser().getMakefile().removeTarget($2);
				}
				catch (const std::out_of_range& ex)
				{
					std::cerr << ex.what() << std::endl;
				}
		 }

target_list: T_LIST
		   {
			if (Parser::getParser().isInteractive())
			{
				std::cout << "Targets list :" << std::endl;
				for (auto pair : Parser::getParser().getMakefile().getTargets())
				{
					std::cout << " - " << pair.second;
				}
			}
		   }

quoted_name: '"' T_WORD '"'
		   {
				strcpy($$, $2);
		   }

quoted_file: '"' T_FILE '"'
		   {
				strcpy($$, $2);
		   }
		   | '"' T_WORD '"'
		   {
				strcpy($$, $2);
		   }

bool_value: T_BOOL_VALUE
		  {
				$$ = $1;
		  }
		  | T_NUMBER
		  {
				$$ = strtol($1, nullptr, 10) != 0;
		  }

%%

