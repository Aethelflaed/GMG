%{

#include <string.h>
#include <stdlib.h>
#include <ctype.h>

#include <iostream>
#include <deque>

#include "Parser.h"
#include "Makefile/Generator.hpp"
#include "Makefile/Util/Output.hpp"

#include "config.h"

extern "C"
{
#ifndef YY_HEADER_EXPORT_START_CONDITIONS
#define YY_HEADER_EXPORT_START_CONDITIONS
#endif

#include "lexer.c.h"

	int yyparse();

    int yywrap()
    {
        return 1;
    }

	void yyerror(const char* str)
	{
		std::cerr << std::endl;
		std::cerr << "Error:        \"" << str << "\"" << std::endl;
		std::cerr << "Scanning:     \"" << yytext << "\"" << std::endl;
		std::cerr << "Parser states stack:" << std::endl;
		const std::deque<int>& states = Parser::getParser().getStates();
		for (auto it = states.rbegin(); it != states.rend(); it++)
		{
#if defined DEBUG || defined DEBUG_STACK
			std::cerr << "\t" << *it << " : " << Parser::getStateName(*it) << std::endl;
#else
			std::cerr << "\t" << *it << std::endl;
#endif
		}
	}
}

%}

%union
{
	char String[256];
	long long Integer;
	unsigned int Natural;
	int Boolean;
	double Double;
	float Float;
}

%token T_EXIT
%token T_HELP

%token T_LIST
%token T_CONFIG_LIST

%token T_ADD
%token T_EDIT
%token T_RM
%token T_SET
%token T_RESET

%token T_DEBUG
%token T_VERBOSE
%token T_OPTIMIZATION

%token <Boolean>T_BOOL_VALUE

%token <String>T_CONFIG_PARAM

%token <String>T_OS
%token T_FLAG
%token T_PATTERN
%token T_FILE
%token T_MODE

%token T_PATH
%token T_DEPENDENCY

%token T_TARGET
%token T_CONFIG
%token T_TOOL

%token T_CONFIG_END
%token T_TARGET_END
%token T_TARGET_TOOL_END
%token T_TOOL_END

%token T_MODULE
%token T_VERSION

%token T_TYPE
%token <Natural>T_TARGET_TYPE

%token <String>T_FILENAME
%token <String>T_NUMBER
%token <String>T_WORD

%type <Natural>config_bool_option

%type <Natural>help_command

%type <String>quoted_name
%type <String>quoted_file
%type <Boolean>bool_value

%start configurations

%%

configurations: /* empty */
			  | configurations configuration
	{
		Parser::getParser().prompt();
	}

configuration: target
			 | target_tool
			 | config
			 | tool
			 | help
			 | newline
			 | exit

exit: T_EXIT
   {
		if (Parser::getParser().isInteractive())
		{
			std::cout << "Bye!" << std::endl;
		}

		Parser::getParser().getMakefileGenerator().generate();

		exit(0);
   }

/* Help */
help: T_HELP help_command newline
	{
		Parser::getParser().help($2);
	}
	| T_HELP newline
	{
		Parser::getParser().help(0);
	}

help_command: T_CONFIG		{ $$ = T_CONFIG;		}
			| T_DEPENDENCY	{ $$ = T_DEPENDENCY;	}
			| T_EXIT		{ $$ = T_EXIT;			}
			| T_MODULE		{ $$ = T_MODULE;		}
			| T_TARGET		{ $$ = T_TARGET;		}
			| T_TOOL		{ $$ = T_TOOL;			}
			| T_TYPE		{ $$ = T_TYPE;			}
			| T_VERSION		{ $$ = T_VERSION;		}

/* Tool */
tool: T_TOOL T_ADD quoted_name quoted_name newline
	{
		Parser::getParser().setTool(&Makefile::Tool::addTool($3, $4));
		Parser::getParser().pushState(TOOL);
	}
	| T_TOOL T_RM quoted_name newline
	{
		try
		{
			Makefile::Tool::removeTool($3);
		}
		catch (const std::invalid_argument& ex)
		{
			yyerror(ex.what());
		}
	}
	| T_TOOL T_EDIT quoted_name newline
	{
		try
		{
			Parser::getParser().setTool(&Makefile::Tool::getTool($3));
		}
		catch (const std::invalid_argument& ex)
		{
			yyerror(ex.what());
		}
		Parser::getParser().pushState(TOOL);
	}
	| T_TOOL T_LIST newline
	{
		Parser::getParser().getTool().list(std::cout);
	}
	| tool_command

tool_command: T_RESET T_FLAG newline
	{
		Parser::getParser().getTool().resetFlags();
	}
			| T_RESET T_DEBUG T_FLAG newline
	{
		Parser::getParser().getTool().resetDebugFlags();
	}
			| T_RESET T_FILE T_PATTERN newline
	{
		Parser::getParser().getTool().resetFilePatterns();
	}
			| T_SET T_VERBOSE T_FLAG quoted_file newline
	{
		Parser::getParser().getTool().setVerboseFlag($4);
	}
			| T_SET T_OPTIMIZATION T_FLAG quoted_file newline
	{
		Parser::getParser().getTool().setOptimizationFlag($4);
	}
			| T_SET T_OS T_PATH quoted_file newline
	{
		Parser::getParser().getTool().setPathForOS(Makefile::Config::getOSValue($2), $4);
	}
			| T_ADD T_FLAG quoted_file newline
	{
		Parser::getParser().getTool().addFlag($3);
	}
			| T_ADD T_DEBUG T_FLAG quoted_file newline
	{
		Parser::getParser().getTool().addDebugFlag($4);
	}
			| T_ADD T_FILE T_PATTERN quoted_file newline
	{
		Parser::getParser().getTool().addFilePattern($4);
	}
			| T_RM T_FLAG quoted_file newline
	{
		try
		{
			Parser::getParser().getTool().removeFlag($3);
		}
		catch (const std::invalid_argument& ex)
		{
			yyerror("No such flag");
		}
	}
			| T_RM T_DEBUG T_FLAG quoted_file newline
	{
		try
		{
			Parser::getParser().getTool().removeDebugFlag($4);
		}
		catch (const std::invalid_argument& ex)
		{
			yyerror("No such flag");
		}
	}
			| T_RM T_FILE T_PATTERN quoted_file newline
	{
		try
		{
			Parser::getParser().getTool().removeFilePattern($4);
		}
		catch (const std::invalid_argument& ex)
		{
			yyerror("No such file pattern");
		}
	}
			| T_TOOL_END newline

target_tool: T_SET T_OPTIMIZATION T_MODE bool_value newline
	{
		Parser::getParser().getTargetTool().setOptimizationMode($4);
	}
		   | T_SET T_VERBOSE T_MODE bool_value newline
	{
		Parser::getParser().getTargetTool().setVerboseMode($4);
	}
		   | T_SET T_DEBUG T_MODE bool_value newline
	{
		Parser::getParser().getTargetTool().setDebugMode($4);
	}
			| T_TARGET_TOOL_END newline

/* Configuration */
config: T_CONFIG newline
	{
		Parser::getParser().pushState(CONFIG);
	}
	  | T_CONFIG_END newline
	  | config_command

config_command: config_bool_param
			  | config_path_param
			  | config_param
			  | config_list

config_bool_param: T_SET config_bool_option bool_value newline
	{
		Makefile::Config& config = Parser::getParser().getCurrentConfig();
		if ($2  == T_DEBUG)
		{
			config.setDebug($3 != 0);
		}
		else if ($2 == T_VERBOSE)
		{
			config.setVerbose($3 != 0);
		}
	}

config_bool_option: T_DEBUG			{ $$ = T_DEBUG; }
				  | T_VERBOSE		{ $$ = T_VERBOSE; }
				  | T_OPTIMIZATION	{ $$ = T_OPTIMIZATION; }

config_path_param: T_ADD T_CONFIG_PARAM T_PATH quoted_file newline
	{
		Makefile::Config& config = Parser::getParser().getCurrentConfig();
		char firstChar = (char) tolower((int) $2[0]);
		if (firstChar == 'i')
		{
			config.addIncludePath($4);
		}
		else
		{
			config.addLibraryPath($4);
		}
	}

config_param: T_ADD T_CONFIG_PARAM quoted_file newline
	{
		Parser::getParser().getCurrentConfig().addLibrary($3);
	}

config_list: T_CONFIG_LIST newline
	{
		if (Parser::getParser().isInteractive())
		{
			std::cout << Parser::getParser().getCurrentConfig() << std::endl;
		}
	}

/* Targets configuration */

target:	T_TARGET target_command
	  | target_mode

/* Target commands : add, edit, remove, list */

target_command: target_add
			  | target_edit
			  | target_rm
			  | target_list

target_add: T_ADD quoted_name newline
	{
		try
		{
			Parser& parser = Parser::getParser();
			parser.setTarget(&(parser.getMakefileGenerator().addTarget($2)));
			parser.pushState(TARGET);
		}
		catch (const std::invalid_argument& ex)
		{
			yyerror(ex.what());
		}
	}

target_edit: T_EDIT quoted_name newline
	{
		try
		{
			Parser& parser = Parser::getParser();
			parser.setTarget(&parser.getMakefileGenerator().getTarget($2));
			parser.pushState(TARGET);
		}
		catch (const std::out_of_range& ex)
		{
			yyerror(ex.what());
		}
	}

target_rm: T_RM quoted_name newline
	{
		try
		{
			Parser::getParser().getMakefileGenerator().removeTarget($2);
		}
		catch (const std::out_of_range& ex)
		{
			yyerror(ex.what());
		}
	}

target_list: T_LIST newline
	{
		if (Parser::getParser().isInteractive())
		{
			std::cout << "Targets list :" << std::endl;
			for (auto pair : Parser::getParser().getMakefileGenerator().getTargets())
			{
				std::cout << *(pair.second) << std::endl;
			}
		}
	}

/* Target configuration, i.e. specific to a target */

target_mode: T_TARGET_END newline
		   | T_LIST newline
	{
		if (Parser::getParser().isInteractive())
		{
			std::cout << Parser::getParser().getTarget() << std::endl;
		}
	}
		   | target_mode_param
		   | target_mode_add_values
		   | target_mode_rm_values

target_mode_param: T_SET T_VERSION T_NUMBER newline
	{
		Parser::getParser().getTarget().setVersion($3);
	}
				 | T_SET T_TYPE T_TARGET_TYPE newline
	{
		Parser::getParser().getTarget().setType(Makefile::TargetType($3));
	}

target_mode_add_values: T_ADD T_MODULE quoted_file newline
	{
		Parser::getParser().getTarget().addModule($3);
	}
					  | T_ADD T_DEPENDENCY quoted_name newline
	{
		Parser::getParser().getTarget().addDependency($3);
	}
					  | T_ADD T_TOOL quoted_name newline
	{
		try
		{
			Parser::getParser().setTargetTool(&Parser::getParser().getTarget().addTool($3));
		}
		catch (const std::invalid_argument& ex)
		{
			yyerror(ex.what());
		}
		Parser::getParser().pushState(TARGET_TOOL);
	}

target_mode_rm_values: T_RM T_MODULE quoted_file newline
	{
		try
		{
			Parser::getParser().getTarget().removeModule($3);
		}
		catch (const std::out_of_range& ex)
		{
			yyerror(ex.what());
		}
	}
					 | T_RM T_DEPENDENCY quoted_name newline
	{
		try
		{
			Parser::getParser().getTarget().removeDependency($3);
		}
		catch (const std::out_of_range& ex)
		{
			yyerror(ex.what());
		}
	}
					 | T_RM T_TOOL quoted_name newline
	{
		Parser::getParser().getTarget().removeTool($3);
		Parser::getParser().popState();
	}

/* Values */

quoted_name: '"' T_WORD '"'
		   {
				strcpy($$, $2);
		   }

quoted_file: '"' T_FILENAME '"'
		   {
				strcpy($$, $2);
		   }
		   | '"' T_WORD '"'
		   {
				strcpy($$, $2);
		   }

bool_value: T_BOOL_VALUE
		  {
				$$ = $1;
		  }
		  | T_NUMBER
		  {
				$$ = strtol($1, nullptr, 10) != 0;
		  }

newline: '\n'

%%

